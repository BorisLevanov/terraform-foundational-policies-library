import "tfplan/v2" as tfplan
import "strings"

allComputeInstances = filter tfplan.resource_changes as _, resource_changes {
	resource_changes.type is "google_compute_instance" and
		resource_changes.mode is "managed" and
		resource_changes.change.actions is ["create"]
}

allComputeInstanceTemplates = filter tfplan.resource_changes as _, resource_changes {
	resource_changes.type is "google_compute_instance_template" and
		resource_changes.mode is "managed" and
		resource_changes.change.actions is ["create"]
}

print("CIS 4.4: Ensure 'Enable connecting to serial ports' is not enabled for VM Instance")

serial_port_enable_is_disabled_in_compute_instance_metadata = rule {
	all allComputeInstances as _, instance {
		strings.to_lower(instance.change.after.metadata["serial-port-enable"]) is "false"
	}
}

serial_port_enable_is_disabled_in_compute_instance_template_metadata = rule {
	all allComputeInstanceTemplates as _, template {
		strings.to_lower(template.change.after.metadata["serial-port-enable"]) is "false"
	}
}

main = rule {
	serial_port_enable_is_disabled_in_compute_instance_metadata and
	serial_port_enable_is_disabled_in_compute_instance_template_metadata
}
