import "tfplan/v2" as tfplan

allContainerClusters = filter tfplan.resource_changes as _, resource_changes {
	resource_changes.type is "google_container_cluster" and
		resource_changes.mode is "managed" and
		resource_changes.change.actions is ["create"]
}

allNodePools = filter tfplan.resource_changes as _, resource_changes {
	resource_changes.type is "google_container_node_pool" and
		resource_changes.mode is "managed" and
		resource_changes.change.actions is ["create"]
}

print("CIS 7.17: Ensure default Service account is not used for Project access in Kubernetes Clusters")

cluster_service_account_is_enabled = rule {
	all allContainerClusters as _, cluster {
		all cluster.change.after.node_config as _, node_config {
			keys(node_config) contains "service_account"
		}
	}
}

pool_service_account_is_enabled = rule {
	all allNodePools as _, pool {
		all pool.change.after.node_config as _, node_config {
			keys(node_config) contains "service_account"
		}
	}
}

main = rule {
	cluster_service_account_is_enabled and
	pool_service_account_is_enabled
}
