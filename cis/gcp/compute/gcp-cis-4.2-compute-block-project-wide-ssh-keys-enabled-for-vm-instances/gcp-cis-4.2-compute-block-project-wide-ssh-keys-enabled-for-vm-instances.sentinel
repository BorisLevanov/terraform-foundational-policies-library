import "tfplan/v2" as tfplan
import "strings"

allComputeInstances = filter tfplan.resource_changes as _, resource_changes {
	resource_changes.type is "google_compute_instance" and
		resource_changes.mode is "managed" and
		resource_changes.change.actions is ["create"]
}

allComputeInstanceTemplates = filter tfplan.resource_changes as _, resource_changes {
	resource_changes.type is "google_compute_instance_template" and
		resource_changes.mode is "managed" and
		resource_changes.change.actions is ["create"]
}

print("CIS 4.2: Ensure 'Block Project-wide SSH keys' are enabled for VM instances")

block_project_ssh_keys_is_enabled_in_compute_instance_metadata = rule {
	all allComputeInstances as _, instance {
		strings.to_lower(instance.change.after.metadata["block-project-ssh-keys"]) is "true"
	}
}

block_project_ssh_keys_is_enabled_in_compute_instance_template_metadata = rule {
	all allComputeInstanceTemplates as _, template {
		strings.to_lower(template.change.after.metadata["block-project-ssh-keys"]) is "true"
	}
}

main = rule {
	block_project_ssh_keys_is_enabled_in_compute_instance_metadata and
	block_project_ssh_keys_is_enabled_in_compute_instance_template_metadata
}
