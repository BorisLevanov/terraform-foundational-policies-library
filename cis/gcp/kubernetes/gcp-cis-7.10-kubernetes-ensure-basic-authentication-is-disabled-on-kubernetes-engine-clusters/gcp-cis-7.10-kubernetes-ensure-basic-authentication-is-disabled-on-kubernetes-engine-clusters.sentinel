import "tfplan/v2" as tfplan

allContainerClusters = filter tfplan.resource_changes as _, resource_changes {
	resource_changes.type is "google_container_cluster" and
		resource_changes.mode is "managed" and
		(resource_changes.change.actions contains "create" or
			resource_changes.change.actions contains "update")
}

print("CIS 7.10: Ensure Basic Authentication is disabled on Kubernetes Engine Clusters")

deny_undefined_master_auth = rule {
	all allContainerClusters as _, cluster {
		keys(cluster.change.after) contains "master_auth"
	}
}

basic_authentication_is_disabled = rule when deny_undefined_master_auth is true {
	all allContainerClusters as _, cluster {
		all cluster.change.after.master_auth as _, master_auth {
			keys(master_auth) not contains "username" and
				keys(master_auth) not contains "password"
		}
	}
}

main = rule {
	deny_undefined_master_auth and
	basic_authentication_is_disabled
}
