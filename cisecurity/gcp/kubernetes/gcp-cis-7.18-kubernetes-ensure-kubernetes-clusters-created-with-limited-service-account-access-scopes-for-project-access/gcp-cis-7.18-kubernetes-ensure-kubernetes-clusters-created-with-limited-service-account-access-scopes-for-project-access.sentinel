import "tfplan/v2" as tfplan

supportedAccessScopes = [
	"https://www.googleapis.com/auth/logging.write",
	"https://www.googleapis.com/auth/monitoring",
	"https://www.googleapis.com/auth/devstorage.read_only",
	"storage-ro",
	"logging-write",
	"monitoring",
]

allContainerClusters = filter tfplan.resource_changes as _, resource_changes {
	resource_changes.type is "google_container_cluster" and
		resource_changes.mode is "managed" and
		resource_changes.change.actions is ["create"]
}

allNodePools = filter tfplan.resource_changes as _, resource_changes {
	resource_changes.type is "google_container_node_pool" and
		resource_changes.mode is "managed" and
		resource_changes.change.actions is ["create"]
}

print("CIS 7.18: Ensure Kubernetes Clusters created with limited service account Access scopes for Project access")

cluster_supported_access_scope_is_configured = rule {
	all allContainerClusters as _, cluster {
		all cluster.change.after.node_config as _, node_config {
			all node_config.oauth_scopes as oauth_scopes {
				oauth_scopes in supportedAccessScopes
			}
		}
	}
}

pool_supported_access_scope_is_configured = rule {
	all allNodePools as _, pool {
		all pool.change.after.node_config as _, node_config {
			all node_config.oauth_scopes as oauth_scopes {
				oauth_scopes in supportedAccessScopes
			}
		}
	}
}

main = rule {
	cluster_supported_access_scope_is_configured and
	pool_supported_access_scope_is_configured
}
